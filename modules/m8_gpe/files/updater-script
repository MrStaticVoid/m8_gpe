ui_print("- Extracting tools");
package_extract_dir("tools", "/tmp/tools");
set_perm_recursive(0, 0, 0755, 0755, "/tmp/tools");

ui_print("- Writing boot.img");
package_extract_file("boot.img", "/dev/block/platform/msm_sdcc.1/by-name/boot");

ui_print("- Writing system.img");
package_extract_file("system.img", "/dev/block/platform/msm_sdcc.1/by-name/system");

ui_print("- Growing system.img to fill partition");
run_program("/tmp/tools/fsck.ext4", "-fy", "/dev/block/platform/msm_sdcc.1/by-name/system");
run_program("/tmp/tools/resize2fs", "/dev/block/platform/msm_sdcc.1/by-name/system");
run_program("/tmp/tools/fsck.ext4", "-fy", "/dev/block/platform/msm_sdcc.1/by-name/system");

ui_print("- Installing SuperSU");
package_extract_dir("supersu", "/tmp/supersu");
set_perm(0, 0, 0755, "/tmp/supersu/installer");
run_program("/tmp/supersu/installer", "", "../../../dev/null", "/tmp/supersu/supersu.zip");

run_program("/sbin/mount", "/system");

ui_print("- Installing Busybox");
run_program("/sbin/cp", "/tmp/tools/busybox", "/system/xbin/busybox");
set_perm(0, 0, 0755, "/system/xbin/busybox");
run_program("/system/xbin/busybox", "--install", "/system/xbin");

ui_print("- Configuring for Verizon");
package_extract_file("build.prop.extra", "/tmp/build.prop.extra");
run_program("/sbin/sh", "-c", "cat /tmp/build.prop.extra >> /system/build.prop");

# Enable CDMA modes
run_program("/system/xbin/sed", "-i", "s/^ro.telephony.default_network=.*/ro.telephony.default_network=10/; s/^#telephony.lteOnCdmaDevice/telephony.lteOnCdmaDevice/", "/system/build.prop");

run_program("/sbin/cp", "/tmp/tools/sqlite3", "/system/xbin/sqlite3");
set_perm(0, 0, 0755, "/system/xbin/sqlite3");

package_extract_dir("init.d", "/system/etc/init.d");
set_perm_recursive(0, 0, 0755, 0755, "/system/etc/init.d");
run_program("/sbin/sh", "-c", "echo '\n# Added by m8_gpe --jlee\nrun-parts /system/etc/init.d' >> /system/etc/usf_post_boot.sh");

unmount("/system");
